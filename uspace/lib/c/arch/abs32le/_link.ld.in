STARTUP(LIBC_PATH/arch/UARCH/src/entry.o)
ENTRY(__entry)

PHDRS {
#ifdef LOADER
	interp PT_INTERP;
	text PT_LOAD FILEHDR PHDRS FLAGS(5);
#else
	text PT_LOAD FLAGS(5);
#endif
	data PT_LOAD FLAGS(6);
}

SECTIONS {
#ifdef LOADER
	. = 0x70001000 + SIZEOF_HEADERS;
	
	.interp : {
		*(.interp);
	} :interp :text
#else
	. = 0x1000 + SIZEOF_HEADERS;
#endif
	
	/* Make sure the code is aligned reasonably */
	. = ALIGN(., 16);
	
	.text : {
		*(.text .text.*);
		*(.rodata .rodata.*);
	} :text
	
	. = . + 0x1000;
	
	.data : {
		*(.data);
		*(.data.rel*);
	} :data
	
	.tdata : {
		_tdata_start = .;
		*(.tdata);
		*(.gnu.linkonce.tb.*);
		_tdata_end = .;
		_tbss_start = .;
		*(.tbss);
		_tbss_end = .;
	} :data
	
	_tls_alignment = ALIGNOF(.tdata);
	
	.sbss : {
		*(.scommon);
		*(.sbss);
	}
	
	.bss : {
		*(COMMON);
		*(.bss);
	} :data
	
	/DISCARD/ : {
		*(*);
	}
}
