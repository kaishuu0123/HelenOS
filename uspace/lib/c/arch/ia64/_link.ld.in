STARTUP(LIBC_PATH/arch/UARCH/src/entry.o)
ENTRY(__entry)

PHDRS {
#ifdef LOADER
	interp PT_INTERP;
	text PT_LOAD FLAGS(5);
#else
	text PT_LOAD FLAGS(5);
#endif
	data PT_LOAD FLAGS(6);
}

SECTIONS {
#ifdef LOADER
	. = 0x800000000 + SIZEOF_HEADERS;
	
	.interp : {
		*(.interp);
	} :interp :text
#else
	. = 0x4000 + SIZEOF_HEADERS;
#endif
	
	/* Make sure the code is aligned reasonably */
	. = ALIGN(., 16);
	
	.init : {
		*(.init);
	} :text
	
	.text : {
		*(.text .text.*);
		*(.rodata .rodata.*);
	} :text
	
	. = . + 0x4000;
	
	.got : {
		/* Tell the linker where we expect GP to point. */
		__gp = .;
		*(.got .got.*);
	} :data
	
	.data : {
		*(.opd);
		*(.data .data.*);
		*(.sdata);
	} :data
	
	.tdata : {
		_tdata_start = .;
		*(.tdata);
		_tdata_end = .;
		_tbss_start = .;
		*(.tbss);
		_tbss_end = .;
	} :data
	
	_tls_alignment = ALIGNOF(.tdata);
	
	.bss : {
		*(.sbss);
		*(.scommon);
		*(COMMON);
		*(.bss);
	} :data
	
	/DISCARD/ : {
		*(*);
	}
}
